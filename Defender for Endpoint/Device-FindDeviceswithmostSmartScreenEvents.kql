//Find the devices in your environment triggering the most Defender SmartScreen events

//Data connector required for this query - M365 Defender - Device* tables

DeviceEvents
| where TimeGenerated > ago (30d)
| where ActionType startswith "SmartScreen"
| extend Experience = tostring(AdditionalFields.Experience)
| where isnotempty(Experience)
| summarize
    ['Count of SmartScreen Events']=count(),
    ['List of SmartScreen Event Types']=make_set(Experience)
    by DeviceName
| sort by ['Count of SmartScreen Events'] desc 

//Advanced Hunting query

//Data connector required for this query - Advanced Hunting license

DeviceEvents
| where Timestamp > ago(30d)
| where ActionType startswith "SmartScreen"
| extend AF = parse_json(AdditionalFields)
| extend Experience = tostring(AF.Experience)
| where isnotempty(Experience)
| summarize
    ['Count of SmartScreen Events']=count(),
    ['List of SmartScreen Event Types']=make_set(Experience)
    by DeviceName
| sort by ['Count of SmartScreen Events'] desc 